# Author: Lucas Vilas-Boas
# Year: 2025
# Repo: https://github.com/lucoiso/luvk

SET(LIBRARY_NAME luvk)

CMAKE_MINIMUM_REQUIRED(VERSION 3.28)
PROJECT(${LIBRARY_NAME} VERSION 0.0.1 LANGUAGES CXX)

SET(CMAKE_CXX_STANDARD 23)
SET(CMAKE_CXX_STANDARD_REQUIRED ON)

IF (POLICY CMP0048)
    CMAKE_POLICY(SET CMP0048 NEW)
ENDIF ()

SET(LUVK_SOURCE_BASE_DIRECTORY ${CMAKE_CURRENT_SOURCE_DIR}/src)
SET(LUVK_INCLUDE_BASE_DIRECTORY ${CMAKE_CURRENT_SOURCE_DIR}/include/)

FILE(GLOB_RECURSE SOURCE_FILES CONFIGURE_DEPENDS "${LUVK_SOURCE_BASE_DIRECTORY}/*.cpp")
FILE(GLOB_RECURSE HEADER_FILES CONFIGURE_DEPENDS "${LUVK_INCLUDE_BASE_DIRECTORY}/luvk/**/*.hpp")

ADD_LIBRARY(${LIBRARY_NAME} SHARED ${SOURCE_FILES})
SET_TARGET_PROPERTIES(${LIBRARY_NAME} PROPERTIES LINKER_LANGUAGE CXX)

TARGET_INCLUDE_DIRECTORIES(${LIBRARY_NAME} PRIVATE ${LUVK_SOURCE_BASE_DIRECTORY})
TARGET_INCLUDE_DIRECTORIES(${LIBRARY_NAME} PUBLIC ${LUVK_INCLUDE_BASE_DIRECTORY})

TARGET_COMPILE_DEFINITIONS(${LIBRARY_NAME} PRIVATE BUILD_DLL)

IF (WIN32)
    TARGET_COMPILE_DEFINITIONS(${LIBRARY_NAME} PUBLIC VK_USE_PLATFORM_WIN32_KHR)
    TARGET_COMPILE_DEFINITIONS(${LIBRARY_NAME} PUBLIC NOMINMAX)
ELSEIF (MACOS)
    TARGET_COMPILE_DEFINITIONS(${LIBRARY_NAME} PUBLIC VK_USE_PLATFORM_MACOS_MVK)
    TARGET_COMPILE_OPTIONS(${LIBRARY_NAME} PUBLIC -fexperimental-library)
ELSEIF (UNIX)
    TARGET_COMPILE_DEFINITIONS(${LIBRARY_NAME} PUBLIC VK_USE_PLATFORM_XCB_KHR)
ENDIF (WIN32)

TARGET_COMPILE_DEFINITIONS(${LIBRARY_NAME} PUBLIC VK_NO_PROTOTYPES=1)
TARGET_COMPILE_DEFINITIONS(${LIBRARY_NAME} PUBLIC VMA_STATIC_VULKAN_FUNCTIONS=0)
TARGET_COMPILE_DEFINITIONS(${LIBRARY_NAME} PUBLIC VMA_DYNAMIC_VULKAN_FUNCTIONS=1)

INCLUDE(FetchContent)

SET(VULKAN_HEADERS_ENABLE_TESTS OFF CACHE BOOL "" FORCE)
SET(VULKAN_HEADERS_ENABLE_INSTALL OFF CACHE BOOL "" FORCE)
SET(VULKAN_HEADERS_ENABLE_MODULE OFF CACHE BOOL "" FORCE)

FETCHCONTENT_DECLARE(vulkan
                     GIT_REPOSITORY https://github.com/KhronosGroup/Vulkan-Headers.git
                     GIT_TAG v1.4.335
)
FETCHCONTENT_MAKEAVAILABLE(vulkan)

SET(VOLK_HEADERS_ONLY OFF CACHE BOOL "" FORCE)
SET(VOLK_INSTALL OFF CACHE BOOL "" FORCE)
SET(VOLK_PULL_IN_VULKAN OFF CACHE BOOL "" FORCE)

FETCHCONTENT_DECLARE(volk
                     GIT_REPOSITORY https://github.com/zeux/volk.git
                     GIT_TAG 34d6fff593000645e6a0415a888198767c288842
)
FETCHCONTENT_MAKEAVAILABLE(volk)

TARGET_SOURCES(${LIBRARY_NAME} PRIVATE ${volk_SOURCE_DIR}/volk.c)
TARGET_INCLUDE_DIRECTORIES(${LIBRARY_NAME} PUBLIC ${volk_SOURCE_DIR})
TARGET_INCLUDE_DIRECTORIES(volk PUBLIC ${vulkan_SOURCE_DIR}/include)

SET(SLANG_LIB_TYPE SHARED CACHE STRING "" FORCE)
SET(SLANG_ENABLE_TESTS OFF CACHE BOOL "" FORCE)
SET(SLANG_ENABLE_EXAMPLES OFF CACHE BOOL "" FORCE)

INCLUDE(cmake/FetchSlang.cmake)
FETCH_SLANG(VERSION 2024.14.4 COMPONENTS slang)

TARGET_INCLUDE_DIRECTORIES(${LIBRARY_NAME} PUBLIC ${slang_SOURCE_DIR}/include)

SET(VMA_ENABLE_INSTALL OFF)
SET(VMA_BUILD_DOCUMENTATION OFF)
SET(VMA_BUILD_SAMPLES OFF)

FETCHCONTENT_DECLARE(vma
                     GIT_REPOSITORY https://github.com/GPUOpen-LibrariesAndSDKs/VulkanMemoryAllocator.git
                     GIT_TAG v3.3.0
)
FETCHCONTENT_MAKEAVAILABLE(vma)

TARGET_INCLUDE_DIRECTORIES(${LIBRARY_NAME} PUBLIC ${vma_SOURCE_DIR}/include)
TARGET_LINK_LIBRARIES(${LIBRARY_NAME} PUBLIC volk slang)

INSTALL(FILES $<TARGET_RUNTIME_DLLS:${LIBRARY_NAME}> TYPE BIN)